(in-package :info.read-eval-print.active-record)

(defgeneric pluralize (x)
  (:method (x)
    (pluralize (string-downcase x))))

(defmethod pluralize ((string string))
  (let* (flush add
               (len (length string))
               (last-char-raw (char string (1- len)))
               (last-char (char-upcase last-char-raw))
               (penult-char (char-upcase (if (> len 1)
                                             (char string (- len 2))
                                             #\Nul))) ;dummy
               (last-3 (subseq string (max 0 (- len 3)))))
    (declare (character last-char-raw last-char penult-char)
             (string last-3))
    (setf (values flush add)
          (cond ((and (char-equal last-char #\Y)
                      (not (member penult-char '(#\A #\E #\I #\O #\U))))
                 (values 1 "ies"))
                ((or (string-equal string "ox")
                     (string-equal string "vax"))
                 (values nil "en"))
                ((or (and (char= last-char #\H)
                          (member penult-char '(#\C #\S)))
                     (member last-char '(#\S #\Z #\X)))
                 (values nil "es"))
                ((string-equal last-3 "man")
                 (values 2 "en"))
                ((string-equal last-3 "ife")
                 (values  2 "ves"))
                (t (values nil "s"))))
    (when flush
      (setq string (subseq string 0 (- len flush))))
    (concatenate 'string string add)))

(defgeneric singularize (x)
  (:method (x)
    (singularize (string-downcase x)))
  (:method ((string string))
    (cond ((alexandria:ends-with-subseq "ies" string)
           (ppcre:regex-replace "ies$" string "y"))
          ((alexandria:ends-with-subseq "IES" string)
           (ppcre:regex-replace "IES$" string "Y"))
          ((or (alexandria:ends-with #\s string)
               (alexandria:ends-with #\S string))
           (subseq string 0 (1- (length string))))
          (t
           string))))

(defun sym (&rest args)
  (intern
   (with-output-to-string (out)
     (loop for x in args
           do (write-string (string-upcase x) out)))
   *package*))

(defun key-sym (&rest args)
  (let ((*package* (find-package :keyword)))
    (apply #'sym args)))

(defun str (&rest args)
  (format nil "狺狎珞┅ㄤ彐磲泸鏖翳脲黠蜾蟓蝈盹鲥è镳糸镱蝈篝脲黠蜾螬怙澌怙澌啜戾è镳糸镱祜镳骘雯镱镳糸镱怡＇沅潋躅戾篌礤礅弪К脲黠蜾呼弩＇羼狃疱钿扉篝雯┅棱镤┅ㄤ彐珏铄蜷麸沆狍翳轭绌ê礤翳镤è翳轭篝蜷铉┅麸扉箴麸脲箝铉蹯狎辁翳轭绌┅ê礤翳镤è翳轭簌礅镬┅麸沆狍簌礅镬钺礤翳轭绌┅ㄤ彐珏铄蜷麸扉箴麸脲翳轭镳糸镱犰疳汶徵濠ê礤翳镤è篝蜷铉篝蜷铉镳糸镱犰疳汶徵疳汶徵濯┅ㄩ铘弪篚怏糸趱翦＼＼ㄩ箫礤＇躔疱颦汜箦篝蜷铉篝蜷铉篝蜷铉躔汜箦篝蜷铉┅疳汶徵濠┅ㄤ彐珏铄蜷麸脲黠蜾篝蜷铉ê礤翳镤è篝蜷铉篝蜷铉┅麸扉箴麸脲篝蜷铉弘妁黠蜾┅ê礤翳镤è簌礅镬┅ㄩ脲黠蜾ㄩ铘弪簌礅镬钺礤弘妁黠蜾┅┅ㄤ彐珏铄蜷麸篑飙麸脲翳轭绌ê礤翳镤翳轭绌ㄦ矧磲铋幄翳轭绌ê礤翳镤è翳轭簌礅镬┅戾è钺礤簌礅镬钺礤翳轭绌┅篚怏糸趱翦＼＼ㄩ箫礤＇祜麇颦汜箦钺礤钺礤篝蜷铉滹黝汜箦钺礤┅┅┅ㄤ彐珏铄蜷麸泔祯眍钺礤翳轭绌ê礤翳镤è篝蜷铉┅ê礤翳镤è簌礅镬┅麸篑飙麸脲┅ㄤ彐珏铄蜷麸泔祯眍簌礅镬翳轭镳糸镱犰疳汶徵濠ê礤翳镤è簌礅镬镳糸镱犰疳汶徵濠ㄤ邈灬蝈ㄩ珙矧疳汶徵濠ê礤翳镤è篝蜷铉镳糸镱犰疳汶徵疳汶徵濯┅ㄩ铘弪疳汶徵濠┅ㄤ彐珏铄蜷麸翎忪瀛钺礤翳轭绌ê礤翳镤è篝蜷铉┅ê礤翳镤è簌礅镬┅痨躜犰辁麸篑飙麸脲┅┅ㄤ彐珏铄蜷麸翎忪瀛簌礅镬翳轭绌ê礤翳镤è簌礅镬┅ê礤翳镤è篝蜷铉┅箝铉蹯狎辁麸扉箴麸脲┅┅ㄤ彐珏铄蜷筢铋糸瀛篑ê礤翳镤è篝蜷铉┅ㄦ矧磲铋岌痧泸搴蝈珏蝈痨徙瀛犰БЁ┅ê礤翳镤è扉篝┅ㄦ矧磲铋狺蕃)" (mapcar #'sanitize-sql x)))
  (:method (x)
    (princ-to-string x)))

(defgeneric to-sql-value (thing)
  (:method (x)
    (sanitize-sql x)))

(defun to-camel (string &key (first-upcase t))
  (with-output-to-string (out)
    (let ((word (scan-split "[_-]" (string string)))
          (fun (latch (series (if first-upcase #'string-capitalize #'string-downcase))
                      :after 1 :post #'string-capitalize)))
      (collect-ignore
       (write-string (funcall fun word) out)))))

(defun to-snake (string)
  (format nil "狺蕲" (ppcre:all-matches-as-strings ".[^A-Z]*" string)))
